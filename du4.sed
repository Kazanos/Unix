#!/bin/sed -Enf
:point
/[^0-9]*[0-9]*[0-9][ ]?\+[ ]?[0-9][0-9]*[^0-9]*/ {
	s|^| |
	h
	s|.*[^0-9]([0-9]*[0-9])[ ]?\+[ ]?([0-9][0-9]*).*|\1 \2|
	s|.*|,&|
	
	:double
	s|(.*)(,[0-9]*)([0-9]) ([0-9]*)([0-9])|\1\3\5\2 \4|
	t double
	
	:leftzero
	s|(.*)(, [0-9]*)([0-9])|\10\3\2|
	t leftzero
	
	:rightzero
        s|(.*)(,[0-9]*)([0-9])|\1\30\2|
        t rightzero
	s|([0-9]*).*|\1|
	
	s|([0-9][0-9])([0-9]*)|,0\1:\2|
	:addition
	s|(,[^,]*)1([^:]*:)|\1X\2|
	t addition
	s|(,[^,]*)2([^:]*:)|\1XX\2|
	t addition
	s|(,[^,]*)3([^:]*:)|\1XXX\2|
	t addition
	s|(,[^,]*)4([^:]*:)|\1XXXX\2|
	t addition
	s|(,[^,]*)5([^:]*:)|\1XXXXX\2|
	t addition
	s|(,[^,]*)6([^:]*:)|\1XXXXXX\2|
	t addition
	s|(,[^,]*)7([^:]*:)|\1XXXXXXX\2|
	t addition
	s|(,[^,]*)8([^:]*:)|\1XXXXXXXX\2|
	t addition
	s|(,[^,]*)9([^:]*:)|\1XXXXXXXXX\2|
	t addition
	s|(,[^,]*)0([^:]*:)|\1\2|
	t addition
	
	s|(,[X]*)XXXXXXXXXX:|\11:|
	t one
	s|([^:]):|\10:|
	:one

	s|,([01]):|,0\1:|
	s|,X([01]):|,1\1:|
	s|,XX([01]):|,2\1:|
	s|,XXX([01]):|,3\1:|
	s|,XXXX([01]):|,4\1:|
	s|,XXXXX([01]):|,5\1:|
	s|,XXXXXX([01]):|,6\1:|
	s|,XXXXXXX([01]):|,7\1:|
	s|,XXXXXXXX([01]):|,8\1:|
	s|,XXXXXXXXX([01]):|,9\1:|

	s|([0-9]*,)([0-9])([0-9]):([0-9][0-9])([0-9]*)|\2\1\3\4:\5|
	/,[0-9][0-9][0-9]:/ b addition
	s|([0-9]*),([0-9])1:|1\2\1|
	s|([0-9]*),([0-9])0:|\2\1|

	s|$|,|
	G
	s|([0-9]*),(.*[^0-9])[0-9]*[0-9][ ]?\+[ ]?[0-9][0-9]*(.*)|\2\1\3|
	s|\n||
	s| ||

	b point
}
p
